apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def getVersionCode = { name ->
    Integer code = 0
    name = name.replaceAll('v', '')
    String[] components = name.split("\\.")
    Integer i = components.size() - 1

    for (component in components) {
        code += Math.pow(1000, i) * (component as Integer)
        i--;
    }

    return code
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags', '--long'
        standardOutput = stdout
    }

    stdout = stdout.toString().trim()
    stdout = stdout.replaceAll(stdout.charAt(stdout.indexOf('-')).toString(), '.')

    return stdout[0..(stdout.lastIndexOf('.') - 1)]
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.converter"
        minSdkVersion 23
        targetSdkVersion 29
        versionName getVersionName()
        versionCode getVersionCode(versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        free {
            dimension "version"
            applicationId "com.example.converter.free"
            versionName getVersionName()
        }
        premium {
            dimension "version"
            applicationId "com.example.converter.premium"
            versionName getVersionName() + "-premium"
        }
    }

    sourceSets {
        free {
            java.srcDirs("src/main/java", "src/free/java")
        }
        premium {
            java.srcDirs("src/premium/java", "src/main/java")
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}